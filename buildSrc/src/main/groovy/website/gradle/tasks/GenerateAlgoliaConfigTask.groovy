/*
 *  Licensed to the Apache Software Foundation (ASF) under one
 *  or more contributor license agreements.  See the NOTICE file
 *  distributed with this work for additional information
 *  regarding copyright ownership.  The ASF licenses this file
 *  to you under the Apache License, Version 2.0 (the
 *  "License"); you may not use this file except in compliance
 *  with the License.  You may obtain a copy of the License at
 *
 *    https://www.apache.org/licenses/LICENSE-2.0
 *
 *  Unless required by applicable law or agreed to in writing,
 *  software distributed under the License is distributed on an
 *  "AS IS" BASIS, WITHOUT WARRANTIES OR CONDITIONS OF ANY
 *  KIND, either express or implied.  See the License for the
 *  specific language governing permissions and limitations
 *  under the License.
 */
package website.gradle.tasks

import groovy.json.JsonOutput
import groovy.transform.CompileStatic

import org.gradle.api.Project
import org.gradle.api.file.RegularFileProperty
import org.gradle.api.provider.Property
import org.gradle.api.tasks.Input
import org.gradle.api.tasks.Internal
import org.gradle.api.tasks.OutputFile
import org.gradle.api.tasks.TaskAction
import org.gradle.api.tasks.TaskProvider

@CompileStatic
abstract class GenerateAlgoliaConfigTask extends GrailsWebsiteTask {

    static final String NAME = 'generateAlgoliaConfig'
    static final String INDEX_NAME = 'grails_site_search'

    @Internal
    final String description = 'Generates the public Algolia browser configuration'

    @OutputFile
    abstract RegularFileProperty getOutputFile()

    @Input
    abstract Property<String> getApplicationId()

    @Input
    abstract Property<String> getSearchApiKey()

    static TaskProvider<GenerateAlgoliaConfigTask> register(Project project, String name = NAME) {
        project.tasks.register(name, GenerateAlgoliaConfigTask) {
            it.outputFile.set(
                    project.layout.buildDirectory
                            .file('dist/javascripts/algolia-config.js')
            )
            it.applicationId.set(
                    project.providers
                            .environmentVariable('ALGOLIA_APP_ID')
                            .orElse('')
            )
            it.searchApiKey.set(
                    project.providers
                            .environmentVariable('ALGOLIA_SEARCH_API_KEY')
                            .orElse('')
            )
        }
    }

    @TaskAction
    void generate() {
        def appId = applicationId.get()
        def searchApiKey = searchApiKey.get()
        def jsonAppId = JsonOutput.toJson(appId)
        def jsonSearchApiKey = JsonOutput.toJson(searchApiKey)
        def jsonIndexName = JsonOutput.toJson(INDEX_NAME)
        outputFile.get().asFile.with {
            parentFile.mkdirs()
            text = "window.GRAILS_ALGOLIA_CONFIG = { appId: ${jsonAppId}, searchApiKey: ${jsonSearchApiKey}, indexName: ${jsonIndexName} };\n"
        }
    }
}
