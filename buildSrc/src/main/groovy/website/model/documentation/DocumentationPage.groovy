/*
 *  Licensed to the Apache Software Foundation (ASF) under one
 *  or more contributor license agreements.  See the NOTICE file
 *  distributed with this work for additional information
 *  regarding copyright ownership.  The ASF licenses this file
 *  to you under the Apache License, Version 2.0 (the
 *  "License"); you may not use this file except in compliance
 *  with the License.  You may obtain a copy of the License at
 *
 *    https://www.apache.org/licenses/LICENSE-2.0
 *
 *  Unless required by applicable law or agreed to in writing,
 *  software distributed under the License is distributed on an
 *  "AS IS" BASIS, WITHOUT WARRANTIES OR CONDITIONS OF ANY
 *  KIND, either express or implied.  See the License for the
 *  specific language governing permissions and limitations
 *  under the License.
 */
package website.model.documentation

import groovy.transform.CompileDynamic
import groovy.transform.CompileStatic

import org.yaml.snakeyaml.Yaml

import static website.utils.RenderUtils.renderHtml

@CompileStatic
class DocumentationPage {

    /**
     * Renders a single documentation category as an HTML guide group.
     * Displays the category's icon, title, and a list of linked items.
     *
     * @param category the documentation category to render
     * @return HTML string representing the category section
     */
    @CompileDynamic
    private static String renderCategory(DocumentationCategory category) {
        renderHtml {
            div(class: 'guide-group') {
                div(class: 'guide-group-header') {
                    img(src: category.image, alt: category.title)
                    h2(category.title)
                }
                ul {
                    category.items.each { item ->
                        li {
                            a(href: item.url, item.title)
                        }
                    }
                }
            }
        }
    }

    /**
     * Renders the documentation links section for a specific Grails version.
     * Includes links to the User Guide and API Reference.
     *
     * @param version the Grails version string (e.g., "6.2.0", "snapshot")
     * @return HTML string with documentation links for the specified version
     */
    @CompileDynamic
    static String renderDocumentation(String version) {
        renderHtml {
            div(class: 'guide-group') {
                if (version) {
                    div(class: 'guide-group-header') {
                        img(
                                src: '[%url]/images/documentation.svg',
                                alt: "Grails Version ($version)"
                        )
                        h2(
                                DocumentationPage.resolveDocumentationName(version)
                        )
                    }
                    ul {
                        li {
                            a(
                                    href: "https://grails.apache.org/docs/$version/",
                                    'User Guide'
                            )
                        }
                        li {
                            a(
                                    href: "https://grails.apache.org/docs/$version/api/",
                                    'API Reference'
                            )
                        }
                    }
                }
            }
        }
    }

    /**
     * Resolves a human-readable documentation title based on the version string.
     * Determines whether the version is a Snapshot, Milestone, Release Candidate, or Latest release.
     *
     * <p>Delegates to {@link ReleaseVersion} so that both legacy dot-style qualifiers
     * ({@code "3.0.0.M1"}, {@code "1.0.RC1"}) and modern dash-style qualifiers
     * ({@code "7.0.0-M1"}, {@code "7.1.0-RC1"}) get the right label. The bare
     * literal {@code "snapshot"} (used by the snapshot section of the rendered
     * pages) bypasses parsing.
     *
     * @param version the Grails version string
     * @return formatted documentation title (e.g., "Latest Version (6.2.0) Documentation")
     */
    static String resolveDocumentationName(String version) {
        String label = 'Latest'
        if (version == null) {
            return "$label Version ($version) Documentation".toString()
        }
        if (version.equalsIgnoreCase('snapshot') || version.toLowerCase().contains('snapshot')) {
            label = 'Snapshot'
        } else {
            ReleaseVersion parsed = ReleaseVersion.build(version)
            if (parsed?.getSnapshot()?.isMilestone()) {
                label = 'Milestone'
            } else if (parsed?.getSnapshot()?.isReleaseCandidate()) {
                label = 'Release Candidate'
            }
        }
        "$label Version ($version) Documentation".toString()
    }

    /**
     * Generates the main documentation page HTML content using the same
     * multi-version card-grid layout as the redesigned downloads page:
     *
     * <ul>
     *   <li><strong>Current Versions</strong> - one card per active minor line
     *       linking to its User Guide + API Reference. Today this renders
     *       7.1.0 + 7.0.10 side-by-side; once 8.0.0 ships, 8.0 + 7.1 + 7.0
     *       = three cards.</li>
     *   <li><strong>Pre-release (Apache-released)</strong> - cards for any
     *       milestone / RC that hasn't been superseded by a stable. Apache
     *       release policy requires the milestone/RC documentation to be
     *       linked even though those versions aren't featured on the home
     *       page.</li>
     *   <li><strong>Snapshot</strong> - the rolling /docs/snapshot/ build.</li>
     *   <li><strong>Older Versions</strong> - the three legacy dropdowns
     *       (Single-page, User Guide, API Reference) for every stable docs
     *       URL since Grails 1.2.0.</li>
     *   <li><strong>Modules</strong> - the GORM / Security / Upgrade / Testing /
     *       Views / Async / Database / Redis / IDE category boxes, in a two-column
     *       footer.</li>
     * </ul>
     *
     * @param releases the YAML file containing Grails release information
     * @param modules the YAML file containing module/category definitions
     * @return complete HTML string for the documentation page
     */
    @CompileDynamic
    static String mainContent(File releases, File modules) {
        List<String> currentLines = SiteMap.activeMinorLines(releases)
        Map<String, ReleaseVersion> latestPerLine = SiteMap.latestStablePerMinorLine(releases)
        Map<Integer, ReleaseVersion> preReleasesPerMajor = SiteMap.latestPreReleasePerMajor(releases)
        def categories = DocumentationPage.categories(modules)
        def missingDocsCriteria = [
                { it.startsWith('0') },
                { it.startsWith('1.0') },
                { it == '3.1.16' }
        ]
        def olderVersionOptions = SiteMap.olderVersions(releases)
                .findAll { v -> !missingDocsCriteria.any { crit -> crit(v) } }
                .collect { "<option>$it</option>" }

        renderHtml {
            div(class: 'header-bar chalices-bg') {
                div(class: 'content') {
                    h1('Documentation')
                }
            }
            div(class: 'content') {
                h2(class: 'release-section-header column-header', 'Current Versions')
                if (currentLines.isEmpty()) {
                    p('No stable releases have been recorded yet.')
                } else {
                    div(class: 'release-grid') {
                        currentLines.each { String lineKey ->
                            ReleaseVersion v = latestPerLine[lineKey]
                            if (v != null) {
                                mkp.yieldUnescaped(
                                        DocumentationPage.renderDocumentation(v.versionText)
                                )
                            }
                        }
                    }
                }

                if (!preReleasesPerMajor.isEmpty()) {
                    h2(class: 'release-section-header column-header', 'Pre-release (Apache-released)')
                    p(
                            'Per Apache release policy, milestone and release-candidate documentation is ' +
                            'linked alongside the stable releases. These versions are not featured on the home page.'
                    )
                    div(class: 'release-grid') {
                        preReleasesPerMajor.values().each { ReleaseVersion v ->
                            mkp.yieldUnescaped(
                                    DocumentationPage.renderDocumentation(v.versionText)
                            )
                        }
                    }
                }

                h2(class: 'release-section-header column-header', 'Snapshot')
                div(class: 'release-grid') {
                    mkp.yieldUnescaped(
                            DocumentationPage.renderDocumentation('snapshot')
                    )
                }

                h2(class: 'release-section-header column-header', 'Older Versions')
                p('Browse previous versions\' documentation since Grails 1.2.0.')
                script(type: 'text/javascript') {
                    mkp.yieldUnescaped(
                            '''
                            function redirectToDocs(selectEl, urlTemplate) {
                                if (selectEl.selectedIndex === 0) {
                                    return; // No version selected
                                }
                                window.location.href = urlTemplate.replace('{v}', selectEl.value);
                            }
                            '''
                    )
                }
                div(class: 'older-versions') {
                    div(class: 'version-selector') {
                        h4('Single Page - User Guide')
                        select(onchange: "redirectToDocs(this, 'https://grails.apache.org/docs/{v}/guide/single.html')") {
                            option('Select a version')
                            mkp.yieldUnescaped(olderVersionOptions)
                        }
                    }
                    div(class: 'version-selector') {
                        h4('User Guide')
                        select(onchange: "redirectToDocs(this, 'https://grails.apache.org/docs/{v}')") {
                            option('Select a version')
                            mkp.yieldUnescaped(olderVersionOptions)
                        }
                    }
                    div(class: 'version-selector') {
                        h4('API Reference')
                        select(onchange: "redirectToDocs(this, 'https://grails.apache.org/docs/{v}/api')") {
                            option('Select a version')
                            mkp.yieldUnescaped(olderVersionOptions)
                        }
                    }
                }

                h2(class: 'release-section-header column-header', 'Modules')
                div(class: 'two-columns') {
                    div(class: 'odd column') {
                        mkp.yieldUnescaped(
                                DocumentationPage.renderCategory(categories.find {
                                    it.title == 'GORM - Data Access Toolkit'
                                })
                        )
                        mkp.yieldUnescaped(
                                DocumentationPage.renderCategory(categories.find {
                                    it.title == 'Security'
                                })
                        )
                    }
                    div(class: 'column') {
                        ['Upgrade', 'Testing', 'Views', 'Async', 'Database', 'Redis', 'IDE'].each { title ->
                            mkp.yieldUnescaped(
                                    DocumentationPage.renderCategory(categories.find {
                                        it.title == title
                                    })
                            )
                        }
                    }
                }
            }
        }
    }

    /**
     * Parses the modules YAML file and groups documentation items by category.
     * Each category contains metadata (title, image, description, URL) and a list of items.
     *
     * @param modules the YAML file containing module definitions
     * @return collection of {@link DocumentationCategory} objects with their items populated
     */
    @CompileDynamic
    private static Collection<DocumentationCategory> categories(File modules) {
        def model = modules.newInputStream().withCloseable { is ->
            new Yaml().load(is) as Map
        }
        Map<String, DocumentationCategory> byCategory = [:].withDefault { key ->
            new DocumentationCategory(title: key)
        }
        (model.modules as Map).each { k, v ->
            def cat = byCategory[v['category']]
            cat.with {
                image = v['categoryImage'] ?: null
                description = v['categoryDescription'] ?: null
                url = v['categoryUrl'] ?: null
            }
            cat.items << new DocumentationItem(url: v['url'], title: v['title'])
        }
        byCategory.values()
    }

    /**
     * Represents a documentation category containing related documentation items.
     * Categories group items like "GORM", "Security", "Testing", etc.
     */
    @CompileStatic
    static class DocumentationCategory {
        String description
        String image
        String title
        String url
        List<DocumentationItem> items = []
    }

    /**
     * Represents a single documentation item within a category.
     * Contains a title and URL linking to the documentation resource.
     */
    @CompileStatic
    static class DocumentationItem {
        String title
        String url
    }
}
