/*
 *  Licensed to the Apache Software Foundation (ASF) under one
 *  or more contributor license agreements.  See the NOTICE file
 *  distributed with this work for additional information
 *  regarding copyright ownership.  The ASF licenses this file
 *  to you under the Apache License, Version 2.0 (the
 *  "License"); you may not use this file except in compliance
 *  with the License.  You may obtain a copy of the License at
 *
 *    http://www.apache.org/licenses/LICENSE-2.0
 *
 *  Unless required by applicable law or agreed to in writing,
 *  software distributed under the License is distributed on an
 *  "AS IS" BASIS, WITHOUT WARRANTIES OR CONDITIONS OF ANY
 *  KIND, either express or implied.  See the License for the
 *  specific language governing permissions and limitations
 *  under the License.
 */
package website.gradle.tasks

import groovy.time.TimeCategory
import groovy.transform.CompileDynamic
import groovy.transform.CompileStatic
import jakarta.annotation.Nonnull
import jakarta.annotation.Nullable
import jakarta.validation.constraints.NotNull
import java.util.regex.Matcher
import java.util.regex.Pattern

import org.gradle.api.Project
import org.gradle.api.file.DirectoryProperty
import org.gradle.api.file.RegularFileProperty
import org.gradle.api.provider.ListProperty
import org.gradle.api.provider.Property
import org.gradle.api.tasks.CacheableTask
import org.gradle.api.tasks.Input
import org.gradle.api.tasks.InputDirectory
import org.gradle.api.tasks.InputFile
import org.gradle.api.tasks.Internal
import org.gradle.api.tasks.OutputDirectory
import org.gradle.api.tasks.PathSensitive
import org.gradle.api.tasks.PathSensitivity
import org.gradle.api.tasks.TaskAction
import org.gradle.api.tasks.TaskProvider

import website.gradle.GrailsWebsiteExtension
import website.model.ContentAndMetadata
import website.model.Page
import website.model.documentation.ReleaseVersion
import website.model.documentation.SiteMap
import website.utils.DateUtils

import static groovy.io.FileType.FILES

@CompileStatic
@CacheableTask
abstract class RenderSiteTask extends GrailsWebsiteTask {

    @Internal
    final String description =
            'Build Grails website - generates pages with HTML entries in pages and build/temp, ' +
            'renders blog and RSS feed, copies assets and generates a sitemap'

    public static final String NAME = 'renderSite'

    private static final String YOUTUBE_WATCH = 'https://www.youtube.com/watch?v='
    private static final String COLON = ':'
    private static final String SEPARATOR = '---'
    private static final int TWITTER_CARD_PLAYER_WIDTH = 560
    private static final int TWITTER_CARD_PLAYER_HEIGHT = 315
    private static final String KAPA_WIDGET = '''<script
    async
    src="https://widget.kapa.ai/kapa-widget.bundle.js"
    data-website-id="d804a9f2-51a2-414c-97f7-12f2a1ba4609"
    data-project-name="Apache Grails"
    data-project-color="#3F4346"
    data-font-family="system-ui,-apple-system,BlinkMacSystemFont,Roboto,Helvetica,Arial,Segoe UI,sans-serif,Apple Color Emoji,Segoe UI Emoji,Segoe UI Symbol;"
    data-project-logo="https://grails.apache.org/images/grails.png"
    data-modal-override-open-id="ask-ai-input"
    data-modal-override-open-class="search-input"
    data-user-analytics-fingerprint-enabled="true"
    data-modal-title="Apache Grails AI Assistant"
    data-modal-example-questions-title="Try asking me..."
    data-modal-example-questions="How does database migration work?,How does Spring Security work?"
    data-button-text="Ask AI"
    data-button-text-color="#feb672"
    data-button-text-font-size="0"
    data-button-bg-color="#3F4346"
    data-button-height="44px"
    data-button-width="44px"
    data-button-image-height="24"
    data-button-image-width="24"
    data-button-padding="10px"
    data-button-border-radius="50%"
    data-button-position-bottom="12px"
    data-button-position-right="12px"
    data-launcher-button-text="Ask AI"
    data-launcher-button-font-size="0"
    data-launcher-button-background-color="#3F4346"
    data-launcher-button-height="44px"
    data-launcher-button-width="44px"
    data-launcher-button-image-height="24"
    data-launcher-button-image-width="24"
    data-launcher-button-padding="10px"
    data-launcher-button-border-radius="50%"
    data-launcher-button-bottom="12px"
    data-launcher-button-right="12px"
    data-modal-header-bg-color="#FFFFFF"
    data-modal-title-color="#255AA8"
    data-consent-required="true"></script>'''

    @InputFile
    @PathSensitive(PathSensitivity.RELATIVE)
    abstract RegularFileProperty getDocument()

    @InputFile
    @PathSensitive(PathSensitivity.RELATIVE)
    abstract RegularFileProperty getReleases()

    @InputDirectory
    @PathSensitive(PathSensitivity.RELATIVE)
    abstract DirectoryProperty getPagesDir()

    @Input
    abstract Property<String> getTitle()

    @Input
    abstract Property<String> getAbout()

    @Input
    abstract Property<String> getUrl()

    @Input
    abstract ListProperty<String> getKeywords()

    @Input
    abstract Property<String> getRobots()

    @OutputDirectory
    abstract DirectoryProperty getOutputDir()

    @InputDirectory
    @PathSensitive(PathSensitivity.RELATIVE)
    abstract DirectoryProperty getPartialsDir()

    static TaskProvider<RenderSiteTask> register(
            Project project,
            GrailsWebsiteExtension siteExt,
            String name = NAME
    ) {
        project.tasks.register(name, RenderSiteTask) {
            it.about.set(siteExt.description)
            it.document.set(siteExt.template)
            it.keywords.set(siteExt.keywords)
            it.outputDir.set(siteExt.outputDir)
            it.pagesDir.set(siteExt.pagesDir)
            it.releases.set(siteExt.releases)
            it.robots.set(siteExt.robots)
            it.title.set(siteExt.title)
            it.url.set(siteExt.url)
            it.partialsDir.set(siteExt.partialsDir)
        }
    }

    @TaskAction
    void renderSite() {
        def releasesFile = releases.get().asFile
        def latest = SiteMap.latestVersion(releasesFile)
        def versions = SiteMap.olderVersions(releasesFile)
                .reverse()
                .collect { "<option>${it}</option>" }
                .join(' ')
        def heroCards = buildHeroCardsHtml(releasesFile)
        def metaData = siteMeta(
                title.get(),
                about.get(),
                url.get(),
                keywords.get(),
                robots.get(),
                latest.versionText,
                versions,
                heroCards)
        def listOfPages = parsePages(pagesDir.get().asFile)
        listOfPages.addAll(
                parsePages(
                        outputDir.dir('temp').get().asFile
                )
        )
        renderPages(
                metaData,
                listOfPages,
                outputDir.dir('dist').get().asFile,
                document.get().asFile.text,
                partialsDir.isPresent() ? partialsDir.get().asFile : null
        )
    }

    /** First Grails major distributed via the Apache mirrors. Hero cards are
     *  only emitted for majors at or above this; older majors used GitHub
     *  releases that don't have the predictable apache.org/dyn/closer.lua URL
     *  pattern the hero links rely on. */
    private static final int FIRST_APACHE_MAJOR = 7

    /**
     * Renders the home-page hero as one card per active minor line, driven
     * entirely by the SiteMap multi-major helpers. This is what makes the
     * home page scale: today the hero shows 7.0 + 7.1, after 8.0.0 ships it
     * shows 7.0 + 7.1 + 8.0 with no edit to {@code pages/index.html} required.
     * Pre-releases / milestones are intentionally absent here per Apache
     * release-policy guidance - those still surface on /download.html and
     * /documentation.html.
     */
    private static String buildHeroCardsHtml(File releasesFile) {
        List<String> activeLines = SiteMap.activeMinorLines(releasesFile)
        Map<String, ReleaseVersion> latestPerLine = SiteMap.latestStablePerMinorLine(releasesFile)
        if (activeLines.isEmpty()) {
            return ''
        }
        StringBuilder sb = new StringBuilder()
        sb.append('<div class="hero-cards">')
        boolean rendered = false
        for (String lineKey : activeLines) {
            ReleaseVersion v = latestPerLine[lineKey]
            if (v == null || v.major < FIRST_APACHE_MAJOR) {
                continue
            }
            String version = v.versionText
            String downloadUrl = "https://www.apache.org/dyn/closer.lua/grails/core/${version}/distribution/apache-grails-${version}-bin.zip?action=download"
            String docsUrl = "https://grails.apache.org/docs/${version}/"
            sb.append('<div class="hero-card">')
            sb.append('<div class="hero-card-label">Grails ').append(version).append('</div>')
            sb.append('<div class="hero-card-actions">')
            sb.append('<a class="hero-action hero-action-download" href="').append(downloadUrl).append('">Download</a>')
            sb.append('<a class="hero-action hero-action-docs" href="').append(docsUrl).append('">Documentation</a>')
            sb.append('</div>')
            sb.append('</div>')
            rendered = true
        }
        sb.append('</div>')
        rendered ? sb.toString() : ''
    }

    static Map<String, String> siteMeta(
            String title,
            String about,
            String url,
            List<String> keywords,
            String robots,
            String latest,
            String versionsBeforeGrails6,
            String heroCards = ''
    ) {
        return [
            title: title,
            description: about,
            url: url,
            latest: latest,
            events: '',
            versions: versionsBeforeGrails6,
            versionAfterGrails6: versionsBeforeGrails6,
            keywords: keywords.join(','),
            robots: robots,
            hero_cards: heroCards,
        ]
    }

    static void renderPages(
            Map<String,
            String> siteMeta,
            List<Page> listOfPages,
            File outputDir,
            String templateText,
            @Nullable File partialsRoot = null,
            String menuPathPrefix = ''
    ) {
        for (def page : listOfPages) {
            // Default the Open Graph URL to <site><page path>, but let a page
            // override it via its own `ogurl` metadata. Pages parsed from disk
            // carry a root-relative path (e.g. "/faq.html"), so the default is
            // correct for them; generated guide pages (index, tags, categories,
            // versions) carry only a bare leaf filename (e.g. "8.html"), so they
            // set `ogurl` explicitly to the right /guides/... URL.
            def pageMetadata = siteMeta + page.metadata
            if (!pageMetadata.containsKey('ogurl')) {
                pageMetadata = pageMetadata + [ogurl: siteMeta['url'] + page.path]
            }
            def resolvedMetadata = processMetadata(pageMetadata)
            def html = renderHtmlWithTemplateContent(
                    page.content,
                    resolvedMetadata,
                    templateText,
                    partialsRoot
            )
            String pagePath = page.path.replaceFirst('^/+', '')
            String menuPath = menuPathPrefix ?
                    "${menuPathPrefix.replaceAll('/+$', '')}${pagePath == 'index.html' ? '' : "/${pagePath}"}" :
                    page.path
            html = highlightMenu(html, menuPath)
            if (page.bodyClassAttr) {
                html = html.replace('<body>', "<body class='${page.bodyClassAttr}'>")
            }
            saveHtmlToPath(outputDir, html, page.path)
        }
    }

    static void saveHtmlToPath(File outputDir, String html, String filepath) {
        def pageOutput = new File(outputDir.absolutePath).tap { it.mkdirs() }
        def paths = filepath.split('/')
        for (String path : paths) {
            if (path.endsWith('.html')) {
                pageOutput = new File(pageOutput, path)
            } else if (path.trim().isEmpty()) {
                // continue
            } else {
                pageOutput = new File(pageOutput, path).tap { it.mkdirs() }
            }
        }
        pageOutput.setText(html, 'UTF-8')
    }

    static Map<String, String> processMetadata(Map<String, String> siteMeta) {
        def resolvedMetadata = siteMeta
        if (resolvedMetadata.containsKey('CSS')) {
            resolvedMetadata.put(
                    'CSS',
                    "<link rel='stylesheet' href='[%url]" + resolvedMetadata['CSS'] + "'/>"
            )
        } else {
            resolvedMetadata.put('CSS', '')
        }
        if (resolvedMetadata.containsKey('JAVASCRIPT')) {
            resolvedMetadata.put(
                    'JAVASCRIPT',
                    "<script src='" + resolvedMetadata['JAVASCRIPT'] + "'></script>"
            )
        } else {
            resolvedMetadata.put('JAVASCRIPT', '')
        }
        if (!resolvedMetadata.containsKey('HTML header')) {
            resolvedMetadata.put('HTML header', '')
        }
        if (!resolvedMetadata.containsKey('keywords')) {
            resolvedMetadata.put('keywords', '')
        }
        if (!resolvedMetadata.containsKey('description')) {
            resolvedMetadata.put('description', '')
        }
        if (resolvedMetadata['disableKapa'] == 'true') {
            resolvedMetadata.put('kapa', '')
        } else if (!resolvedMetadata.containsKey('kapa')) {
            resolvedMetadata.put('kapa', KAPA_WIDGET)
        }
        if (!resolvedMetadata.containsKey('date')) {
            resolvedMetadata.put(
                    'date',
                    DateUtils.format_MMM_D_YYYY_HHMM(new Date())
            )
        }
        if (!resolvedMetadata.containsKey('robots')) {
            resolvedMetadata.put('robots', 'all')
        }
        resolvedMetadata.put(
                'twittercard',
                twitterCard('summary_large_image')
        )
        if (resolvedMetadata.containsKey('video')) {
            def videoId = parseVideoId(resolvedMetadata)
            if (videoId) {
                resolvedMetadata.put(
                        'twittercard',
                        twitterCard('player') +
                                twitterPlayerHtml(
                                        videoId,
                                        TWITTER_CARD_PLAYER_WIDTH,
                                        TWITTER_CARD_PLAYER_HEIGHT
                                )
                )
            }
        }

        if (!resolvedMetadata.containsKey('ogimage')) {
            if (resolvedMetadata.containsKey('image')) {
                resolvedMetadata.put('ogimage', resolvedMetadata['url'] + '/images/' + resolvedMetadata['image'])
            } else if (resolvedMetadata.containsKey('video') && parseVideoId(resolvedMetadata)) {
                String videoId = parseVideoId(resolvedMetadata)
                resolvedMetadata.put('ogimage', "https://img.youtube.com/vi/${videoId}/maxresdefault.jpg".toString())
            } else {
                resolvedMetadata.put('ogimage', resolvedMetadata['url'] + '/images/grails.png')
            }
        }
        resolvedMetadata
    }

    @Nullable
    static String parseVideoId(Map<String, String> metadata) {
        metadata.containsKey('video') && metadata['video'].startsWith(YOUTUBE_WATCH) ?
                metadata['video'].substring(YOUTUBE_WATCH.length()) :
                null
    }

    @Nullable
    static String parseVideoIframe(Map<String, String> metadata) {
        def videoId = parseVideoId(metadata)
        videoId ?
                '<iframe width="100%" height="560" src="https://www.youtube-nocookie.com/embed/' + videoId + '" frameborder="0"></iframe>' :
                null
    }

    static String twitterPlayerHtml(String videoId, int width, int height) {
        """\
        <meta name='twitter:player' content='https://www.youtube.com/embed/$videoId'/>
        <meta name='twitter:player:width' content='$width'/>
        <meta name='twitter:player:height' content='$height'/>
        """.stripIndent(8)
    }

    static String twitterCard(String cardType) {
        "<meta name='twitter:card' content='$cardType'/>"
    }

    static String highlightMenu(String html, String path) {
        String normalizedPath = path.startsWith('/') ? path : "/$path"
        html.replaceFirst(
                "(<li)(><a href='[^']*${Pattern.quote(normalizedPath)}')",
                "\$1 class='active'\$2"
        )
    }

    static List<Page> parsePages(File pages) {
        List<Page> listOfPages = []
        pages.eachFileRecurse(FILES) { file ->
            if (file.path.endsWith('.html')) {
                def contentAndMetadata = parseFile(file)
                def filename = file.absolutePath.replace(pages.absolutePath, '')
                listOfPages.add(
                        new Page(
                                filename: filename,
                                content: contentAndMetadata.content,
                                metadata: contentAndMetadata.metadata
                        )
                )
            }
        }
        listOfPages
    }

    static ContentAndMetadata parseFile(File file) {
        String line = null
        List<String> lines = []
        Map<String, String> metadata = [:]
        boolean metadataProcessed = false
        int lineCount = 0
        file.withReader { reader ->
            while ((line = reader.readLine()) != null) {
                if (lineCount == 0 && line.startsWith(SEPARATOR)) {
                    continue
                }
                lineCount++
                if (line.startsWith(SEPARATOR)) {
                    metadataProcessed = true
                    continue
                }
                if (!metadataProcessed && line.contains(COLON)) {
                    String metadataKey = line.substring(0, line.indexOf(COLON as String)).trim()
                    String metadataValue = line.substring(line.indexOf(COLON as String) + COLON.length()).trim()
                    metadata[metadataKey] = metadataValue
                }
                line = replaceLineWithMetadata(line, metadata)
                if (metadataProcessed) {
                    lines << line
                }
            }
        }

        !metadataProcessed || lines.isEmpty() ?
                new ContentAndMetadata(metadata: [:] as Map<String, String>, content: file.text) :
                new ContentAndMetadata(metadata: metadata, content: lines.join('\n'))
    }

    @Nonnull
    static String renderHtmlWithTemplateContent(
            @Nonnull @NotNull String html,
            @Nonnull @NotNull Map<String, String> meta,
            @NotNull @Nonnull String templateText,
            @Nullable File partialsRoot = null
    ) {
        def outputHtml = expandPartials(templateText, partialsRoot)
        def result = outputHtml.replace(' data-document>', '>' + html)
        return replaceLineWithMetadata(result, meta)
    }

    /**
     * Replace [%PARTIAL:&lt;name&gt;] tokens with the contents of
     * &lt;partialsRoot&gt;/&lt;name&gt;.html, so shared chrome (header, footer,
     * head scripts) lives in a single source of truth.
     */
    static String expandPartials(String templateText, @Nullable File partialsRoot) {
        if (partialsRoot == null) {
            return templateText
        }
        Pattern token = Pattern.compile(/\[%PARTIAL:([\w\-]+)\]/)
        Matcher m = token.matcher(templateText)
        StringBuilder out = new StringBuilder()
        int last = 0
        while (m.find()) {
            out.append(templateText, last, m.start())
            File partial = new File(partialsRoot, "${m.group(1)}.html")
            if (partial.isFile()) {
                out.append(partial.getText('UTF-8'))
            } else {
                out.append("<!-- missing partial: ${m.group(1)} -->")
            }
            last = m.end()
        }
        out.append(templateText, last, templateText.length())
        out.toString()
    }

    /**
     * Resolves placeholders used by shared partials before a separate
     * renderer, such as the Guide renderer, inserts them into its template.
     */
    static String resolvePartial(String partialText, String siteUrl, boolean disableKapa = false) {
        replaceLineWithMetadata(partialText, [
                url: siteUrl,
                kapa: disableKapa ? '' : KAPA_WIDGET,
        ])
    }

    static String formatDate(String date) {
        DateUtils.format_MMMM_D_YYYY(
                DateUtils.parseDate(date)
        )
    }

    @CompileDynamic
    static String formatDateMinus6Months(String date) {
        use(TimeCategory) {
            DateUtils.format_MMMM_D_YYYY(
                    DateUtils.parseDate(date) - 6.months
            )
        }
    }

    static String URLEncode(String date) {
        URLEncoder.encode(date, 'UTF-8')
    }

    static String replaceLineWithMetadata(String line, Map<String, String> metadata) {
        Map<String, String> m = new HashMap<>(metadata)
        if (m.containsKey('date')) {
            m['date'] = formatDate(m['date'])
            m['6MonthsBackForGitHub'] =
                    '?from=' +
                            URLEncode(formatDateMinus6Months(m['date'])) +
                            '&to=' +
                            URLEncode(formatDate(m['date']))
        }
        for (String metadataKey : m.keySet()) {
            if (line.contains("[%$metadataKey]")) {
                def value = m[metadataKey]
                if ("[%$metadataKey]" == '[%author]') {
                    def authors = value.split(',') as List<String>
                    value = '<span class="author">By ' + authors.join('<br/>') + '</span>'
                    line = line.replaceAll(
                            "\\[%$metadataKey\\]",
                            value
                    )

                } else if ("[%$metadataKey]" == '[%date]') {
                    if (line.contains('<meta')) {
                        line = line.replaceAll(
                                "\\[%$metadataKey\\]",
                                value
                        )
                    } else {
                        value = '<span class="date">' + value + '</span>'
                        line = line.replaceAll(
                                "\\[%$metadataKey\\]",
                                value
                        )
                    }
                } else {
                    line = line.replaceAll(
                            "\\[%$metadataKey\\]",
                            value
                    )
                }
            }
        }
        line
    }
}
